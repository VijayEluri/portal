import org.apache.tools.ant.filters.ReplaceTokens

bundlePath = "$buildDir/dist/"
bundleTmpPath = "$bundlePath/tmp/"
warTmpPath = "$bundleTmpPath/war/"
sqlTmpPath = "$bundleTmpPath/sql/"

dependencies {
  compile project(':portal-webapp')
}

task dist << {
  println "task bundle dist"
  cleanBundle()
  sqlPreparation()
  bundleWar()
  bundleTomcat()
  cleanTmpFolder()
}

def bundleWar() {
  def bundleWarPath = "$bundlePath/devproof-portal-${version}-war"
  mkdir(bundleWarPath)
  copyReadmeAndChanges(bundleWarPath, 'TOMCAT7')
  copyLicences(bundleWarPath)
  copyWar(bundleWarPath)
  copyConfigFiles(bundleWarPath)
  concatAllSqlScriptsAndWrite(bundleWarPath)
  tarGz(bundleWarPath)
}

def bundleTomcat() {
  def bundleTomcatPath = "$bundlePath/devproof-portal-${version}-with-tomcat7"
  mkdir(bundleTomcatPath)
  copyReadmeAndChanges(bundleTomcatPath, 'WAR')
  copyLicences(bundleTomcatPath)
  copyWar(bundleTomcatPath)
  copyConfigFiles(bundleTomcatPath)
  concatAllSqlScriptsAndWrite(bundleTomcatPath)
  def tomcatPath = downloadTomcatAndExtract(bundleTomcatPath)
  deleteDir("$tomcatPath/webapps")
  mkdir("$tomcatPath/webapps")
  copyLibsForTomcat(tomcatPath)
  copyConfigForTomcat(tomcatPath)
  createHsqlDBFiles(bundleTomcatPath, tomcatPath)
  tarGz(bundleTomcatPath)
}

def concatAllSqlScriptsAndWrite(bundlePath) {
  concatCreateSqlAndWriteFile('hsql', bundlePath)
  concatCreateSqlAndWriteFile('mysql', bundlePath)
  concatCreateSqlAndWriteFile('oracle', bundlePath)
  concatUpdateSqlAndWriteFile('hsql', bundlePath)
  concatUpdateSqlAndWriteFile('mysql', bundlePath)
  concatUpdateSqlAndWriteFile('oracle', bundlePath)
}

def tarGz(bundlePath) {
  ant.tar(
      destfile: bundlePath + '.tar',
      basedir: bundlePath
  )
  def bundlePathTarGz = bundlePath + '.tar.gz'
  ant.gzip(
      destfile: bundlePathTarGz,
      src: bundlePath + '.tar'
  )
  delete(bundlePath + '.tar')
  println "Devproof bundled distribution: $bundlePathTarGz"
}

def copyConfigForTomcat(tomcatPath) {
  copy {
    from ('config/tomcat7/hsql/ROOT.xml') {
      filter(ReplaceTokens, tokens: [devproofVersion: project.version])
    }
    into "$tomcatPath/conf/Catalina/localhost/"
  }
}

def copyLibsForTomcat(tomcatPath) {
  copy {
    from 'server/tomcat7/lib/'
    into "$tomcatPath/lib/"
  }
}

def downloadTomcatAndExtract(bundlePath) {
  // url is in root build.gradle
  def downloadedFile = download(bundlePath, tomcatUrl)
  def tarFile = downloadedFile[0..-4]
  def folder = downloadedFile[0..-8]
  ant.gunzip(
    src: downloadedFile,
    dest: tarFile
  )

  ant.untar(
    src: tarFile,
    dest: bundlePath
  )
  delete(downloadedFile)
  delete(tarFile)
  folder  // removes .tar.gz
}

def copyWar(bundlePath) {
  println "copyWar"
  copy {
    from locateWarPath()
    into bundlePath + "/war/"
    rename { String fileName ->
        "devproof-portal-${version}.war"
    }
  }
}

def copyConfigFiles(bundlePath) {
  println "copyConfig"
  copy {
    from 'config'
    into "$bundlePath/config"
  }
}

def copyReadmeAndChanges(bundlePath, readmePostfix) {
  println "copyReadmeAndChanges"
  copy {
    from "README_$readmePostfix"
    into bundlePath
    rename { String fileName ->
        "README"
    }
  }
  copy {
    from "CHANGES"
    into bundlePath
  }
}

def copyLicences(bundlePath) {
  println "copyLicences"
  copy {
    from '/licenses/'
    into bundlePath + '/licenses/'
  }
}

def sqlPreparation() {
  extractWar()
  extractSql()
}

def createHsqlDBFiles(bundleTomcatPath, tomcatPath) {
  def bundleTomcatHsqlPath = "$tomcatPath/data/"
  mkdir(bundleTomcatHsqlPath)
  ant.sql(
      driver: "org.h2.Driver",
      url: "jdbc:h2:file:$bundleTomcatHsqlPath/devproof_data",
      userid: "sa",
      password: "",
      delimiter: "dummy_delimiter_for_one_commit",
      output: "true",
      keepformat: "true",
      autocommit: "true",
      src: "$bundleTomcatPath/sql/install_devproof_hsql.sql",
      classpath: configurations.compile.asPath
  )
}

def cleanBundle() {
  println "cleanBundle"
  deleteDir(bundlePath)
}

def deleteDir(dir) {
  println "cleanBundle"
  file(dir).deleteDir()
}

def cleanTmpFolder() {
  println "clean tmp"
  file(bundlePath + '/tmp/').deleteDir()
}

def locateWarPath() {
  def p = project(':portal-webapp')
  p.buildDir.path + '/libs/' + p.name + '-' + version + '.war'
}

def extractWar() {
  println "extractWar"
  copy {
    from zipTree(locateWarPath())
    into warTmpPath
  }
}

def extractSql() {
  println "extractSql"
  copy {
    jars = "$warTmpPath/WEB-INF/lib/"
    fileTree(jars).each{
      jarFile ->
      include 'sql/*/*.sql'
      from zipTree(jarFile)
      into bundleTmpPath
    }
  }
}

def concatCreateSqlAndWriteFile(type, bundlePath) {
  println "concatCreateSqlAndWriteFile $type"
  def sqlPath = "$bundlePath/sql/"
  mkdir(sqlPath)
  File outputFile = file(sqlPath + '/install_devproof_' + type + '.sql')
  outputFile.append(file(sqlTmpPath + '/create/create_tables_' + type + '_core.sql').text);
  outputFile.append("\n" + file(sqlTmpPath + '/create/insert_core.sql').text);
  def sqlFiles = file(sqlTmpPath + '/create').listFiles().sort()
  // create* files
  sqlFiles.each { File file ->
    def createCore = 'create_tables_' + type + '_core.sql'
    if(file.name.startsWith("create_tables_$type") &&
      file.name != createCore) {
      outputFile.append("\n" + file.text);
    }
  }
  // insert* files
  sqlFiles.each { File file ->
    def insertCore = 'insert_core.sql'
    if(file.name.startsWith('insert_') &&
      file.name != insertCore) {
      outputFile.append("\n" + file.text);
    }
  }
}

def concatUpdateSqlAndWriteFile(type, bundlePath) {
  println "concatUpdateSqlAndWriteFile $type"
  def sqlPath = "$bundlePath/sql/update/"
  def filePrefix = "update_from_"

  mkdir(sqlPath)
  def sqlFiles = file(sqlTmpPath + '/update').listFiles().sort()

  // update* files core
  sqlFiles.each { File file ->
    if(file.name.contains("_${type}_") && file.name.endsWith('_core.sql')) {
      tmp = file.name.minus(filePrefix)
      index = tmp.lastIndexOf("_${type}_")
      def sqlVersion = tmp.substring(0, index)
      def outputFilePath = "$sqlPath${filePrefix}${sqlVersion}_${type}.sql"
      File outputFile = new File(outputFilePath)
      outputFile.append(file.text + "\n");
    }
  }

  // update* files
  sqlFiles.each { File file ->
    if(file.name.contains("_${type}_") && !file.name.endsWith('_core.sql')) {
      tmp = file.name.minus(filePrefix)
      index = tmp.lastIndexOf("_${type}_")
      def sqlVersion = tmp.substring(0, index)
      def outputFilePath = "$sqlPath/${filePrefix}${sqlVersion}_${type}.sql"
      File outputFile = new File(outputFilePath)
      outputFile.append(file.text + "\n");
    }
  }
}

def download(folder, address) {
  println "Downloading... $address"
  ant.get(
    dest: folder,
    src: address
  )
  def filename = address.tokenize("/")[-1]
  def localFilePath = folder + '/' + filename
  localFilePath
}
